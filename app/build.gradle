apply plugin: 'com.android.application'
apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

android {
    ndkVersion "29.0.14206865"

    compileSdk 36

    namespace 'com.limelight'

    defaultConfig {
        minSdk 34
        targetSdk 36

        versionName "13"
        versionCode = 314

        // Generate native debug symbols to allow Google Play to symbolicate our native crashes
        ndk.debugSymbolLevel = 'FULL'

        // Only support 64-bit ARM architecture (remove armv7 and x86 support)
        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a'
        }

        applicationId "com.qwe7002.deragabu"
    }

    buildFeatures {
        buildConfig = true
    }

    packaging {
        resources {
            excludes += ['META-INF/versions/9/OSGI-INF/MANIFEST.MF']
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    lint {
        disable 'MissingTranslation'
        lintConfig file('lint.xml')
    }

    bundle {
        language {
            // Avoid splitting by language, since we allow users
            // to manually switch language in settings.
            enableSplit = false
        }
        density {
            // FIXME: This should not be necessary but we get
            // weird crashes due to missing drawable resources
            // when this split is enabled.
            enableSplit = false
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
}

// Determine Cargo profile based on build type
def isReleaseBuild = gradle.startParameter.taskNames.any { it.toLowerCase().contains('release') }
def cargoProfile = isReleaseBuild ? "release" : "debug"

cargo {
    module = "./src/main/jni/moonlight-core-rs"
    libname = "moonlight_core"
    targets = ["arm64"]
    profile = cargoProfile
    targetDirectory = "./src/main/jni/moonlight-core-rs/target"
}

// Hook Rust compilation into Android build
tasks.configureEach { task ->
    if (task.name == 'mergeDebugJniLibFolders' || task.name == 'mergeReleaseJniLibFolders') {
        task.dependsOn 'cargoBuild'
    }
}

// Also hook for native library tasks
tasks.whenTaskAdded { task ->
    if (task.name == 'stripDebugDebugSymbols' || task.name == 'stripReleaseDebugSymbols') {
        task.dependsOn 'cargoBuild'
    }
}

dependencies {
    implementation 'org.bouncycastle:bcprov-jdk18on:1.83'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.83'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation 'com.github.cgutman:ShieldControllerExtensions:1.0.1'
    implementation 'com.tencent:mmkv:2.3.0'

    // AndroidX Fragment and Preference
    implementation 'androidx.fragment:fragment:1.8.9'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
}
